// Plugin to generate BuildConfig.java
plugins {
  id 'de.fuerstenau.buildconfig' version '1.1.8'
}

apply plugin: 'java'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

group 'kadiary'

// Generate BuildConfig.java with final constants like API_URL
// Constants can be set by CI system variables
buildConfig {
  sourceSets {
    main {
      clsName = 'BuildConfig'      // sets the name of the BuildConfig class
      packageName = project.group  // sets the package of the BuildConfig class,
      buildConfigField 'String', 'DEFAULT_API_URL', System.getenv("API_URL") ?: "my-api-url"
      buildConfigField 'String', 'DEFAULT_API_KEY', System.getenv("API_KEY") ?: "my-api-key"
    }
  }
}

// Generate fat jar with dropbox client library inside
jar {
  manifest {
    attributes 'Main-Class': 'kadiary.Main'
  }
  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {
  // plain diary models
  compile project(':dropbox-diary-model')
  // http-client
  compile 'com.squareup.retrofit2:retrofit:2.4.0'
  // http-client json to object mapper
  compile 'com.squareup.retrofit2:converter-jackson:2.4.0'
  // http-client timeout configurer
  compile 'com.squareup.okhttp3:okhttp:3.10.0'
}
